// Keywords: nucleotide-based, nucleotide sequence

initialize() {
	defineConstant("L", CHR_LENGTH);
	initializeSLiMOptions(nucleotideBased=T);
	setSeed(REP_ID);
	initializeAncestralNucleotides(randomNucleotides(L));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
	initializeMutationTypeNuc("m2", 0.5, "f", SELECTION_COEFF);
	initializeGenomicElementType("g1", c(m1, m2), c(0.9999999, 0.0000001), mmJukesCantor(MUTATION_RATE));
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(REC_RATE);
}
1 {	
	// Output acestral sequence in fasta format
	header = ">rep_" + REP_ID;
	sequence = sim.chromosome.ancestralNucleotides();
	writeFile(OUT_PATH + "rep_" + REP_ID + "/slim/ancestral.fasta", header, append=F);
	writeFile(OUT_PATH + "rep_" + REP_ID + "/slim/ancestral.fasta", sequence, append=T);
	sim.addSubpop("p1", POP_SIZE);
	sim.addSubpop("p2", POP_SIZE);
	p1.setMigrationRates(p2, MIGRATION_RATE);
	p2.setMigrationRates(p1, MIGRATION_RATE);

}
fitness(m2, p2) { return 1/relFitness; }
10000 late() {
	// Output sampled haplotypes
	allIndividuals_p1 = sample(p1.individuals, POP_SIZE, replace=F);
	allIndividuals_p2 = sample(p2.individuals, POP_SIZE, replace=F);
	header = ">rep_" + REP_ID;
	for (sample_id in 1:SAMPLE_SIZE) {
		// p1, genome1		
		g = allIndividuals_p1.genomes[2*sample_id-2];
		sequence = g.nucleotides();
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p1_derived_" + sample_id + "_1.fasta", header, append=F);
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p1_derived_" + sample_id + "_1.fasta", sequence, append=T);
		// p1, genome2
		g = allIndividuals_p1.genomes[2*sample_id-1];
		sequence = g.nucleotides();
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p1_derived_" + sample_id + "_2.fasta", header, append=F);
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p1_derived_" + sample_id + "_2.fasta", sequence, append=T);
		// p2, genome1
		g = allIndividuals_p2.genomes[2*sample_id-2];
		sequence = g.nucleotides();
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p2_derived_" + sample_id + "_1.fasta", header, append=F);
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p2_derived_" + sample_id + "_1.fasta", sequence, append=T);
		// p2, genome2
		g = allIndividuals_p2.genomes[2*sample_id-1];
		sequence = g.nucleotides();
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p2_derived_" + sample_id + "_2.fasta", header, append=F);
		writeFile(OUT_PATH + "rep_" + REP_ID + "/fasta/p2_derived_" + sample_id + "_2.fasta", sequence, append=T);
	}
	
	// Output population-level allele frequencies
	sim.outputMutations(c(sim.mutationsOfType(m1),sim.mutationsOfType(m2)), OUT_PATH + "rep_" + REP_ID + "/slim/mutations.txt");
	sim.outputFixedMutations(OUT_PATH + "rep_" + REP_ID + "/slim/substitutions.txt");
}
